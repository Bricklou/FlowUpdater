plugins {
    id "com.jfrog.bintray" version "1.8.5"
}

apply plugin: 'maven-publish'

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'

    group 'fr.flowarg'

    compileJava {
        options.encoding = 'UTF-8'
        sourceCompatibility = JavaVersion.VERSION_1_8
    }

    repositories {
        jcenter()

        maven {
            url "https://jitpack.io/"
            name = "JitPack"
        }
    }

    task sourcesJar(type: Jar) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    dependencies {
        api 'com.google.code.gson:gson:2.8.6'
        api 'fr.flowarg:flowmultitools:1.2.0'
        implementation 'com.github.FlowArg:PluginLoaderAPI:871c5910c6'
    }
}

version '1.2.4'
archivesBaseName = "flowupdater"

artifacts {
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USERNAME')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava'] 
    pkg {
        repo = 'maven'
        name = 'FlowUpdater'
        userOrg = 'flowarg'
        licenses = ['GPL-3.0']
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/FlowArg/FlowUpdater.git'
        version {
            name = 'v' + project.version
            released = new Date()
            vcsTag = 'v' + project.version
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            pom {
                groupId = project.group
                version = project.version
                artifactId = 'flowupdater'
                name = project.name
                description = 'FlowUpdater is a library for automatically update minecraft versions !'
                url = 'https://github.com/FlowArg/FlowUpdater'

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/FlowArg/FlowUpdater/issues'
                }

                licenses {
                    license {
                        name = 'LICENSE'
                        url = 'https://github.com/FlowArg/FlowUpdater/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}

dependencies {
    compileOnly project(":CurseForgePlugin")
}
