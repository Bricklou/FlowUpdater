allprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    group 'fr.flowarg'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    compileJava {
        options.encoding = 'UTF-8'
        sourceCompatibility = JavaVersion.VERSION_1_8
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url "https://jitpack.io/"
            name = "JitPack"
        }
    }

    task sourcesJar(type: Jar) {
        classifier 'sources'
        from sourceSets.main.allSource
    }
    
    task javadocJar(type: Jar) {
        classifier 'javadoc'
        from javadoc
    }

    dependencies {
        api 'com.google.code.gson:gson:2.8.8'
        api 'fr.flowarg:flowmultitools:1.4.0'
        api 'org.jetbrains:annotations:22.0.0'
    }

    artifacts {
        archives sourcesJar, javadocJar
    }
}

version '1.5.0'
archivesBaseName = "flowupdater"

dependencies {
    // Only for internal tests
    testImplementation 'fr.flowarg:openlauncherlib:3.2.2'
    testImplementation 'org.json:json:20210307'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.0'
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            pom {
                groupId = project.group
                version = project.version
                artifactId = 'flowupdater'
                name = project.name
                description = 'The free and open source solution to update Minecraft.'
                url = 'https://github.com/FlowArg/FlowUpdater'

                scm {
                    connection = 'scm:git:git://github.com/FlowArg/FlowUpdater.git'
                    developerConnection = 'scm:git:ssh://github.com:FlowArg/FlowUpdater.git'
                    url = 'https://github.com/FlowArg/FlowUpdater/tree/master'
                }

                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'flowarg'
                        name = 'Flow Arg'
                        email = 'coutureflorence3@gmail.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {        
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }
}

signing {
    def signingKey = System.getenv("GPG_PRIVATE_KEY")
    def signingPassword = System.getenv("GPG_PASSPHRASE")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

test {
    useJUnitPlatform()
}