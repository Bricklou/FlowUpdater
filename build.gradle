plugins {
    id "com.github.johnrengelman.shadow" version "6.1.0" apply false
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'

    group 'fr.flowarg'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    compileJava {
        options.encoding = 'UTF-8'
        sourceCompatibility = JavaVersion.VERSION_1_8
    }

    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
        maven {
            url "https://jitpack.io/"
            name = "JitPack"
        }
    }

    task sourcesJar(type: Jar) {
        classifier 'sources'
        from sourceSets.main.allSource
    }
    
    task javadocJar(type: Jar) {
        classifier 'javadoc'
        from javadoc
    }

    dependencies {
        api 'com.google.code.gson:gson:2.8.6'
        api 'fr.flowarg:flowmultitools:1.2.5'
        implementation 'com.github.FlowArg:PluginLoaderAPI:94e88e0'
    }
}

version '1.3.5'
archivesBaseName = "flowupdater"

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar
            
            pom {
                groupId = project.group
                version = project.version
                artifactId = 'flowupdater'
                name = project.name
                description = 'FlowUpdater is a library for automatically update minecraft versions !'
                url = 'https://github.com/FlowArg/FlowUpdater'

                scm {
                    connection = 'scm:git:git://github.com/FlowArg/FlowUpdater.git'
                    developerConnection = 'scm:git:ssh://github.com:FlowArg/FlowUpdater.git'
                    url = 'http://github.com/FlowArg/FlowUpdater/tree/master'
                }

                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'flowarg'
                        name = 'Flow Arg'
                        email = 'coutureflorence3@gmail.com'
                    }
                }
            }
        }
    }
    
    repositories {
        maven {
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = project.version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    compileOnly project(":CurseForgePlugin")
    compileOnly project (":OptifinePlugin")

    // Only for internal tests
    testImplementation 'fr.flowarg:openlauncherlib:3.1.+'
}
